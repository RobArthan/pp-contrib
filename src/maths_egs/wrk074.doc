=IGN
********************************************************************************
wrk073.doc: this file is supplementary material for the ProofPower system

Copyright (c) 2004 Lemma 1 Ltd.

This file is supplied under the GNU General Public Licence (GPL) version 2.

See the file LICENSE supplied with ProofPower source for the terms of the GPL
or visit the OpenSource web site at http://www.opensource.org/

Contact: Rob Arthan < rda@lemma-one.com >
********************************************************************************
=TEX
\documentclass[11pt,a4paper]{article}
\usepackage{latexsym}
\usepackage{ProofPower}
\usepackage{A4}

\def\ThmsII#1#2{%
{\vertbarfalse
\begin{minipage}[t]{0.48\hsize}
#1
\end{minipage}
\begin{minipage}[t]{0.48\hsize}
#2
\end{minipage}}}

\def\ThmsIII#1#2#3{%
{\vertbarfalse
\begin{minipage}[t]{0.32\hsize}
#1
\end{minipage}
\begin{minipage}[t]{0.32\hsize}
#2
\end{minipage}
\begin{minipage}[t]{0.32\hsize}
#3
\end{minipage}}}
\def\Hide#1{\relax}
\def\N{\mathbb{N}}
\def\D{\mathbb{D}}
\def\B{\mathbb{B}}
\def\C{\mathbb{C}}
\def\R{\mathbb{R}}
\def\Z{\mathbb{Z}}
\def\Q{\mathbb{Q}}

\def\ExpName{\mbox{{\sf exp}}}
\def\Exp#1{\ExpName(#1)}

\def\LogName{\mbox{{\sf log}}}
\def\Log#1{\LogName(#1)}

\def\SinName{\mbox{{\sf sin}}}
\def\Sin#1{\SinName(#1)}

\def\CosName{\mbox{{\sf cos}}}
\def\Cos#1{\CosName(#1)}

\def\Abs#1{|#1|}

\def\ElemsName{\mbox{{\em Elems}}}
\def\Elems#1{\ElemsName(#1)}

\def\SizeName{\#}
\def\Size#1{\#\left(#1\right)}

%\def\Binomial#1#2{\left(\begin{array}{c}#1\\#2\end{array}\right)}
\def\Binomial#1#2{C_{#2}^{#1}}

\title{Mathematical Case Studies: Some Number Theory}
\author{Rob Arthan\\{\tt rda@lemma-one.com}}
\makeindex
\begin{document}
\vfill
\maketitle
\begin{abstract}
Beginnings of number theory in \ProductHOL.
\end{abstract}
%\newpage
%\tableofcontents
%\newpage
\section{INTRODUCTION}

Some number theory.

\section{THE THEORY {\em numbers}}\label{theory}

\subsection{Preliminaries}
=SML
force_delete_theory "numbers" handle Fail _ => ();
open_theory"fincomb";
new_theory "numbers";
set_merge_pcs["basic_hol1", "'sets_alg", "'ú", "'¯"];
=TEX

\subsection{Divisors in î}
We begin working in the natural numbers and develop the theory of divisors.
We define an infix relation {\em Divides} such that {\em n Divides m} iff. $m$ is a multiple of $n$.
 
=SML
declare_infix(200, "Divides");
¹HOLCONST
Ü Û$DividesÝ : î ­ î ­ BOOL
÷üüüüüü
Ü µn m· n Divides m ¤ ¶k· m = k * n
°


¹HOLCONST
Ü ÛGcdÝ : î ­ î ­ î
÷üüüüüü
Ü µm n·	0 < m ± 0 < n
Ü ´	Gcd m n Divides m
Ü ±	Gcd m n Divides n
Ü ± 	µd·	d Divides m
Ü	±	d Divides n
Ü	´	d Divides Gcd m n
°

The most common textbook account exhibits the g.c.d. of $m$ and $n$ as the smallest positive value of the form $am + bn$.
This requires $a$ and $b$ to range over negative integers.
A slightly less symmetrical alternative is to take the g.c.d. to be the smallest positive value of the form $(am)\;Mod\;n$.
This works over the natural numbers and the following theorems lead up to the proof that this does give the g.c.d.
We have to develop various facts about multiplicative arithmetic over the natural numbers en route.

\ThmsIII{
=GFT
times_eq_0_thm
times_cancel_thm
times_eq_eq_1_thm
times_eq_1_thm
div_mod_1_thm
m_div_mod_m_thm
zero_div_mod_thm
less_div_mod_thm
div_mod_times_cancel_thm
=TEX
}{%
=GFT
mod_clauses
mod_eq_0_thm
divides_0_thm
divides_mod_thm
divides_refl_thm
divides_antisym_thm
divides_trans_thm
mod_plus_homomorphism_thm
mod_times_homomorphism_thm
=TEX
}{%
=GFT
mod_î_exp_thm
gcd_consistent_lemma1
gcd_consistent_lemma2
gcd_consistent_lemma3
gcd_consistent_lemma4
gcd_consistent_lemma5
Gcd_consistent
gcd_def
=TEX
}
\subsection{Indexed Sums and Products in î}
A useful application of the set fold operation is to define the following indexed sum operation. Given an index set, $a$, and a function, $f$, assigning a number to each member of $a$,
=INLINEFT
IndSum‰N a f
=TEX
\ is the indexed sum $\sum_{x\in a}f(x)$, which is defined on any set $a$ in which $f$ has finite support.


¹HOLCONST
Ü ÛIndSum‰NÝ : 'a SET ­ ('a ­ î) ­ î
÷üüüüüü
Ü µf·	IndSum‰N {} f = 0
Ü ±	µx a·	a ¡ {x|³ f x = 0}  Finite ± ³x  a
Ü	´	IndSum‰N ({x} À a) f = f x + IndSum‰N a f
°

We will write $\sum\,a\,f$ as shorthand for
=INLINEFT
IndSum‰N a f
=TEX
.
=SML
declare_alias("“", ¬IndSum‰N®);
=TEX
Similarly, we define indexed products:
=TEX
¹HOLCONST
Ü ÛIndProd‰NÝ : 'a SET ­ ('a ­ î) ­ î
÷üüüüüü
Ü µf·	IndProd‰N {} f = 1
Ü ±	µx a·	a ¡ {x|³ f x = 1}  Finite ± ³x  a
Ü	´	IndProd‰N ({x} À a) f = f x * IndProd‰N a f
°

We will write $\prod\,a\,f$ as shorthand for
=INLINEFT
IndProd‰N a f
=TEX
.
=SML
declare_alias("", ¬IndProd‰N®);
=TEX


\subsection{Real Integral Domains}
In general, an integral domain is a ring without zero divisors.
As we are only concerned with subrings of the reals here, any subring of the reals is an integral domain. 
 
¹HOLCONST
Ü ÛRealIDÝ : ¯ SET SET
÷üüüüüü
Ü µA· A  RealID ¤
Ü	î¯ 1  A
Ü ±	(µx y·x  A ± y  A ´ x + y  A)
Ü ±	(µx·x  A ´ ~x  A)
Ü ±	(µx y·x  A ± y  A ´ x * y  A)
°

The domain of (rational) integers comprises the intersection of all real integral domains:

¹HOLCONST
Ü Ûú‰RÝ : ¯ SET
÷üüüüüü
Ü ú‰R = ¥RealID
°
=SML
declare_alias("ú", ¬ú‰R®);
=TEX
To get started, we prove that ¯ is a real integral domain, as is the intersection of any family of real integral domains, and so in particular is ú, the intersection of all real integral domains.
We then show that ú is precisely the image of the type ú of integers under the injection ú¯:

\ThmsII{
=GFT
¯_real_i_d_thm
¥_real_i_d_thm
=TEX
}{%
=GFT
ú_real_i_d_thm
ú_thm
=TEX
}

\subsection{Real Fields}
A real field is a real integral domain that is closed under taking reciprocals of non-zero elements.

¹HOLCONST
Ü ÛRealFieldÝ : ¯ SET SET
÷üüüüüü
Ü µA· A  RealField ¤
Ü	A  RealID
Ü ±	(µx·x  A \ {î¯ 0} ´ x ›-›1  A)
°

The field of rational numbes is the intersection of all real fields.

¹HOLCONST
Ü Û%bbQ%‰RÝ : ¯ SET
÷üüüüüü
Ü %bbQ%‰R = ¥RealField
°

Following a similar pattern to the last section, we prove that ¯ is a real field, as is the intersection of any family of real fields, in particular, %bbQ%, the intersection of all real fields.
We prove that ú is a subset of %bbQ%.
An explicit formula for the set %bbQ% is given in the next section.

\ThmsII{
=GFT
¯_real_field_thm
¥_real_field_thm
=TEX
}{
=GFT
rat_real_i_d_thm
ú_€_rat_thm
=TEX
}

=SML
declare_alias("%bbQ%", ¬%bbQ%‰R®);
=TEX
\subsection{Fields of Fractions}
The field of fractions of a set $A$, (typically an integral domain) is the intersection of all fields that contain $A$.

¹HOLCONST
Ü ÛFieldOfFractionsÝ : ¯ SET ­ ¯ SET
÷üüüüüü
Ü µA· FieldOfFractions A = ¥{K | K  RealField ± A € K}
°
We prove that the field of fractions of any set is indeed a real field and that %bbQ% is the field of fractions of ú.
We then show that if $A$ is an integral domain, then its field of fractions does indeed comprise precisely the set of fractions $a/b$ with $a, b \in A$ and $b \not = 0$.
Using this we can derive an explicit formula for the set %bbQ%.


\ThmsII{
=GFT
field_of_fractions_field_thm
rat_field_of_fractions_thm
=TEX
}{
=GFT
field_of_fractions_thm
rat_thm
=TEX
}

%\bibliographystyle{fmu}
%\bibliography{fmu}

%\appendix
{%\HOLindexOff
\let\Section\section
\let\subsection\Hide
\def\section#1{\Section{#1}\label{listing}}
\let\subsection\Hide
\include{wrk074.th}}

=TEX

\twocolumn[\section{INDEX}\label{INDEX}]
{\small\printindex}

\end{document} %% COMMENT THIS LINE OUT TO TYPESET THE PROOF SCRIPTS
=TEX
%%%%
%%%%
=SML
val Ûdivides_defÝ = get_spec ¬$Divides®;
val Ûreal_i_d_defÝ = get_spec ¬RealID®;
val Ûú_defÝ = get_spec ¬ú‰R®;
val Ûreal_field_defÝ = get_spec ¬RealField®;
val Ûrat_defÝ = get_spec ¬%bbQ%‰R®;
val Ûfield_of_fractions_defÝ = get_spec ¬FieldOfFractions®;
=TEX
%%%%
%%%% DIVISORS IN î
%%%%
=SML
val Ûdiv_mod_unique_thm1Ý =
	rewrite_rule[taut_rule
		¬µp1 p2 p3· (p1 ´ p2 ´ p3) ¤ (p1 ± p2 ´ p3)®]
	(conv_rule (ONCE_MAP_C(RAND_C(RAND_C (RANDS_C eq_sym_conv))))
		div_mod_unique_thm);
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm n·m * n = 0 ´ m = 0 ² n = 0®);
a(contr_tac);
a(LEMMA_T¬1 ¼ m ± 1 ¼ n® (strip_asm_tac o rewrite_rule[¼_def])
	THEN1 PC_T1 "lin_arith" asm_prove_tac[]);
a(all_var_elim_asm_tac1
	THEN PC_T1 "lin_arith" asm_prove_tac[]);
val Ûtimes_eq_0_thmÝ = save_pop_thm "times_eq_0_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µk m n·0 < k ± k * m = k * n ´ m = n®);
a(contr_tac);
a((LEMMA_T¬m ¼ n ² n ¼ m® (strip_asm_tac o rewrite_rule[¼_def])
	THEN1 rewrite_tac[¼_cases_thm])
		THEN all_var_elim_asm_tac1);
(* *** Goal "1" *** *)
a(lemma_tac¬k*i = 0® THEN1 PC_T1 "lin_arith" asm_prove_tac[]);
a(fc_tac[times_eq_0_thm] THEN all_var_elim_asm_tac1
	THEN1 PC_T1 "lin_arith" asm_prove_tac[]);
(* *** Goal "2" *** *)
a(lemma_tac¬k*i = 0® THEN1 PC_T1 "lin_arith" asm_prove_tac[]);
a(fc_tac[times_eq_0_thm] THEN all_var_elim_asm_tac1
	THEN1 PC_T1 "lin_arith" asm_prove_tac[]);
val Ûtimes_cancel_thmÝ = save_pop_thm "times_cancel_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm n·0 < n ± m * n = n ´ m = 1®);
a(REPEAT strip_tac THEN
	bc_thm_tac (once_rewrite_rule[times_comm_thm] times_cancel_thm));
a(¶_tac¬n® THEN asm_rewrite_tac[]);
val Ûtimes_eq_eq_1_thmÝ = save_pop_thm "times_eq_eq_1_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm n·m * n = 1 ´ m = 1 ± n = 1®);
a(REPEAT µ_tac);
a(cases_tac¬m = 0 ² n = 0 ² m = 1 ² n = 1® THEN_TRY
	(all_var_elim_asm_tac1 THEN PC_T1 "lin_arith" asm_prove_tac[]));
a(LEMMA_T¬2 ¼ m ± 2 ¼ n® (strip_asm_tac o rewrite_rule[¼_def])
	THEN1 PC_T1 "lin_arith" asm_prove_tac[]);
a(LIST_DROP_NTH_ASM_T [3, 4, 5, 6] discard_tac);
a(all_var_elim_asm_tac1 THEN PC_T1 "lin_arith" asm_prove_tac[]);
val Ûtimes_eq_1_thmÝ = save_pop_thm "times_eq_1_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm·m Div 1 = m ± m Mod 1 = 0®);
a(µ_tac);
a(bc_thm_tac div_mod_unique_thm1 THEN rewrite_tac[]);
val Ûdiv_mod_1_thmÝ = save_pop_thm "div_mod_1_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm· 0 < m ´ m Div m = 1 ± m Mod m = 0®);
a(µ_tac THEN ´_tac);
a(bc_thm_tac div_mod_unique_thm1 THEN asm_rewrite_tac[]);
val Ûm_div_mod_m_thmÝ = save_pop_thm "m_div_mod_m_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm·0 < m ´ 0 Div m = 0 ± 0 Mod m = 0®);
a(µ_tac THEN ´_tac);
a(bc_thm_tac div_mod_unique_thm1 THEN asm_rewrite_tac[]);
val Ûzero_div_mod_thmÝ = save_pop_thm "zero_div_mod_thm";
=TEX
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm n·n < m ´ n Div m = 0 ± n Mod m = n®);
a(REPEAT µ_tac THEN ´_tac);
a(bc_thm_tac div_mod_unique_thm1 THEN asm_rewrite_tac[]);
val Ûless_div_mod_thmÝ = save_pop_thm "less_div_mod_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µk m n·0 < k ´ (m*k + n) Div k = m + n Div k ±  (m*k + n) Mod k = n Mod k®);
a(REPEAT µ_tac THEN ´_tac);
a(bc_thm_tac div_mod_unique_thm1
	THEN ALL_FC_T rewrite_tac[mod_less_thm]);
a(rewrite_tac[times_plus_distrib_thm, plus_assoc_thm]);
a(bc_thm_tac div_mod_thm THEN REPEAT strip_tac);
val Ûdiv_mod_times_cancel_thmÝ = save_pop_thm "div_mod_times_cancel_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µk m n·
	0 < k
´	(m*k) Mod k = 0
±	(k*m) Mod k = 0
±	(k*m + n) Mod k = n Mod k
±	(m*k + n) Mod k = n Mod k
±	(k + n) Mod k = n Mod k
±	(n + k) Mod k = n Mod k
±	0 Mod k = 0
±	k Mod k = 0
±	(m Mod k) Mod k = m Mod k
®);
a(REPEAT µ_tac THEN ´_tac);
a(rewrite_tac[µ_elim¬k® times_comm_thm]);
a(ALL_FC_T rewrite_tac[
	div_mod_times_cancel_thm, m_div_mod_m_thm, zero_div_mod_thm]);
a(pure_once_rewrite_tac[prove_rule[]
	¬m*k = m*k + 0 ± k + n = 1*k + n ± n + k = 1*k + n®]);
a(ALL_FC_T pure_rewrite_tac[div_mod_times_cancel_thm]
	THEN ALL_FC_T rewrite_tac[zero_div_mod_thm]);
a(lemma_tac¬m Mod k < k® THEN1 ALL_FC_T rewrite_tac[mod_less_thm]);
a(ALL_FC_T rewrite_tac[less_div_mod_thm]);
val Ûmod_clausesÝ = save_pop_thm "mod_clauses";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm n·
	0 < n
´	(m Mod n = 0 ¤ ¶k·m = k*n)
®);
a(REPEAT strip_tac);
(* *** Goal "1" *** *)
a(¶_tac¬m Div n®);
a(ALL_FC_T (conv_tac o LEFT_C o once_rewrite_conv)[div_mod_thm]);
a(asm_rewrite_tac[]);
(* *** Goal "2" *** *)
a(all_var_elim_asm_tac1 THEN ALL_FC_T rewrite_tac[mod_clauses]);
val Ûmod_eq_0_thmÝ = save_pop_thm "mod_eq_0_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm·m Divides 0 ± (0 Divides m ¤ m = 0)®);
a(REPEAT µ_tac THEN rewrite_tac[divides_def]);
a(¶_tac¬0® THEN rewrite_tac[]);
val Ûdivides_0_thmÝ = save_pop_thm "divides_0_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µn m·
	n Divides m ¤ (if 0 < n then m Mod n else m) = 0
®);
a(REPEAT µ_tac);
a(cases_tac¬³0 < n® THEN asm_rewrite_tac[divides_def]);
(* *** Goal "1" *** *)
a(lemma_tac¬n = 0® THEN1 PC_T1 "lin_arith" asm_prove_tac[]);
a(cases_tac¬m = 0® THEN asm_rewrite_tac[]);
(* *** Goal "2" *** *)
a(ALL_FC_T1 fc_¤_canon rewrite_tac[mod_eq_0_thm]);
val Ûdivides_mod_thmÝ = save_pop_thm "divides_mod_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬ µm· m Divides m ®);
a(REPEAT strip_tac
	THEN rewrite_tac[divides_def]
	THEN ¶_tac ¬1® THEN rewrite_tac[]);
val Ûdivides_refl_thmÝ = save_pop_thm "divides_refl_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬ µm n· n Divides m ± m Divides n ¤ m = n ®);
a(REPEAT µ_tac THEN cases_tac¬n = 0®
	THEN1 asm_rewrite_tac[divides_0_thm]);
a(cases_tac¬m = 0®
	THEN1 (asm_rewrite_tac[divides_0_thm]
		THEN PC_T1 "lin_arith" asm_prove_tac[]));
a(lemma_tac¬0 < n ± 0 < m® THEN1 PC_T1 "lin_arith" asm_prove_tac[]);
a(asm_rewrite_tac[divides_mod_thm]);
a(REPEAT strip_tac);
(* *** Goal "1" *** *)
a(all_fc_tac[arb_gen mod_eq_0_thm]);
a(lemma_tac¬(k'*k)*m = m® THEN1
	(POP_ASM_T (fn th => conv_tac (RIGHT_C(rewrite_conv[th])))
		THEN asm_rewrite_tac[times_assoc_thm]));
a(all_fc_tac[arb_gen times_eq_eq_1_thm]);
a(all_fc_tac[arb_gen times_eq_1_thm]);
a(all_var_elim_asm_tac1 THEN rewrite_tac[]);
(* *** Goal "2" *** *)
a(all_var_elim_asm_tac THEN ALL_FC_T rewrite_tac[mod_clauses]);
(* *** Goal "3" *** *)
a(all_var_elim_asm_tac THEN ALL_FC_T rewrite_tac[mod_clauses]);
val Ûdivides_antisym_thmÝ = save_pop_thm "divides_antisym_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬ µm n k· n Divides m ± m Divides k ´ n Divides k ®);
a(REPEAT µ_tac THEN rewrite_tac[divides_def]
	THEN REPEAT strip_tac);
a(¶_tac¬k''*k'® THEN asm_rewrite_tac[times_assoc_thm]);
val Ûdivides_trans_thmÝ = save_pop_thm "divides_trans_thm";

=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm n k·0 < k ´ (m + n) Mod k = (m Mod k + n Mod k) Mod k®);
a(REPEAT strip_tac);
a(all_fc_tac[div_mod_thm]);
a(TOP_ASM_T (ante_tac o µ_elim¬m®));
a(POP_ASM_T (ante_tac o µ_elim¬n®) THEN REPEAT strip_tac);
a(REPEAT (POP_ASM_T (fn th => conv_tac(LEFT_C(once_rewrite_conv[th])))));
a(rewrite_tac[pc_rule1 "lin_arith" prove_rule[]
	¬µa b c d·(a*k + b) + (c*k + d) = (a+c)*k + b + d®]);
a(ALL_FC_T rewrite_tac[div_mod_times_cancel_thm]);
val Ûmod_plus_homomorphism_thmÝ = save_pop_thm "mod_plus_homomorphism_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm n k·0 < k ´ (m * n) Mod k = ((m Mod k) * (n Mod k)) Mod k®);
a(REPEAT strip_tac);
a(all_fc_tac[div_mod_thm]);
a(TOP_ASM_T (ante_tac o µ_elim¬m®));
a(POP_ASM_T (ante_tac o µ_elim¬n®) THEN REPEAT strip_tac);
a(REPEAT (POP_ASM_T (fn th => conv_tac(LEFT_C(once_rewrite_conv[th])))));
a(rewrite_tac[pc_rule1 "lin_arith" prove_rule[]
	¬µa b c d·(a*k + b) * (c*k + d) = (a*c*k + a*d +b*c)*k + b*d®]);
a(ALL_FC_T rewrite_tac[div_mod_times_cancel_thm]);
val Ûmod_times_homomorphism_thmÝ = save_pop_thm "mod_times_homomorphism_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm n k·0 < k ´ (m ^ n) Mod k = ((m Mod k) ^ n) Mod k®);
a(REPEAT strip_tac);
a(induction_tac¬n® THEN rewrite_tac[î_exp_def]);
a(ALL_FC_T1 fc_¤_canon once_rewrite_tac
	[mod_times_homomorphism_thm]);
a(ALL_FC_T asm_rewrite_tac[mod_clauses]);
val Ûmod_î_exp_thmÝ = save_pop_thm "mod_î_exp_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm n·
	0 < m ± 0 < n ± 0 < m Mod n
´	¶a· 0 < (a*m) Mod n
± 	µb· 0 < (b*m) Mod n ´ (a*m) Mod n ¼ (b*m) Mod n®);
a(REPEAT strip_tac);
a(PC_T1 "predicates" lemma_tac
	¬ ¶i· i  { i | ¶a·0 < (a*m) Mod n ± i = (a*m) Mod n } ®);
(* *** Goal "1" *** *)
a(¶_tac¬m Mod n® THEN REPEAT strip_tac);
a(¶_tac¬1® THEN asm_rewrite_tac[]);
(* *** Goal "2" *** *)
a(all_fc_tac[min__thm]);
a(¶_tac¬a® THEN REPEAT strip_tac);
a(DROP_NTH_ASM_T 4 discard_tac);
a(PC_T1 "predicates" lemma_tac
	¬ (b*m) Mod n  { i | ¶a·0 < (a*m) Mod n ± i = (a*m) Mod n } ®
	THEN1 (REPEAT strip_tac THEN asm_prove_tac[]));
a(all_fc_tac[min_¼_thm]);
a(POP_ASM_T ante_tac THEN asm_rewrite_tac[]);
val Ûgcd_consistent_lemma1Ý = save_pop_thm"gcd_consistent_lemma1";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm n a d·
	0 < m ± 0 < n ± 0 < d
±	m Mod d = 0 ± n Mod d = 0
´	((a*m) Mod n) Mod d = 0
®);
a(REPEAT strip_tac);
a(all_fc_tac[mod_eq_0_thm]);
a(ante_tac(list_µ_elim[¬a*m®, ¬n®] div_mod_thm));
a(strip_tac THEN LEMMA_T
	¬(((a*m) Div n)*n + (a*m) Mod n) Mod d = (a*m) Mod d® ante_tac
	THEN1 POP_ASM_T (rewrite_thm_tac o eq_sym_rule));
a(ALL_FC_T once_rewrite_tac[mod_plus_homomorphism_thm]);
a(all_var_elim_asm_tac1
	THEN rewrite_tac[conv_rule (ONCE_MAP_C eq_sym_conv) times_assoc_thm]
	THEN ALL_FC_T rewrite_tac[mod_clauses]);
val Ûgcd_consistent_lemma2Ý = save_pop_thm"gcd_consistent_lemma2";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µa b m n p r s·
	a*m = b*(n+1) + r
±	m = p*r + s
´	¶q·(q*m) Mod (n+1) = s Mod (n+1)®);
a(REPEAT strip_tac);
a(¶_tac¬n*p*a+1®);
a(scale_nth_asm_tac¬n*p® 2);
a(rewrite_tac[times_assoc_thm, times_plus_distrib_thm] THEN
	POP_ASM_T (asm_rewrite_thm_tac o rewrite_rule[times_assoc_thm]));
a(LEMMA_T ¬
	n*p*(b*(n + 1) + r) + p * r + s =
	(n+1)*(p*b*n + p*r) + s® rewrite_thm_tac THEN1
	PC_T1 "lin_arith" prove_tac[]);
a(rewrite_tac[rewrite_rule[](µ_elim¬n+1®mod_clauses)]);
val Ûgcd_consistent_lemma3Ý = save_pop_thm"gcd_consistent_lemma3";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µa b m n p r s·
	a*m = b*(n+1) + r
±	n + 1 = p*r + s
´	¶q·(q*m) Mod (n+1) = s Mod (n+1)®);
a(REPEAT strip_tac);
a(LEMMA_T¬µx·(x*m) Mod (n + 1) = (x*m + n + 1) Mod (n + 1)®
	rewrite_thm_tac THEN1
		rewrite_tac[rewrite_rule[](µ_elim¬n+1®mod_clauses)]);
a(¶_tac¬n*p*a®);
a(scale_nth_asm_tac¬n*p® 2);
a(rewrite_tac[times_assoc_thm, times_plus_distrib_thm] THEN
	POP_ASM_T (rewrite_thm_tac o rewrite_rule[times_assoc_thm]));
a(LEMMA_T¬µx·x + n + 1 = x + p*r + s® rewrite_thm_tac THEN1
	asm_rewrite_tac[]);
a(LEMMA_T ¬
	n*p*(b*(n+1) + r) + p * r + s =
	(n+1)*(p*b*n + p*r) + s® rewrite_thm_tac THEN1
	PC_T1 "lin_arith" prove_tac[]);
a(rewrite_tac[rewrite_rule[](µ_elim¬n+1®mod_clauses)]);
val Ûgcd_consistent_lemma4Ý = save_pop_thm"gcd_consistent_lemma4";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm n k a·
	0 < m ± 0 < n ± 0 < m Mod n
±	0 < (a*m) Mod n
± 	(µb· 0 < (b*m) Mod n ´ (a*m) Mod n ¼ (b*m) Mod n)
´	m Mod ((a*m) Mod n) = 0 ± n Mod ((a*m) Mod n) = 0®);
a(REPEAT µ_tac THEN ´_tac THEN 
	LEMMA_T ¬1 ¼ n®
		(strip_asm_tac o once_rewrite_rule[plus_comm_thm]
			o rewrite_rule[¼_def]) THEN1
	PC_T1 "lin_arith" asm_prove_tac[]);
a(all_var_elim_asm_tac1 THEN contr_tac);
(* *** Goal "1" *** *)
a(ante_tac(list_µ_elim[ ¬a®, ¬(a*m) Div (i+1)®,
	¬m®, ¬i®, ¬m Div ((a*m) Mod (i+1))®,
	¬(a*m) Mod (i+1)®,
	¬m Mod ((a*m) Mod (i+1))® ]gcd_consistent_lemma3));
a(asm_tac (prove_rule[]¬0 < i + 1®));
a(ALL_FC_T rewrite_tac[
	conv_rule(ONCE_MAP_C eq_sym_conv) div_mod_thm]);
a(lemma_tac¬m Mod ((a*m) Mod (i + 1)) < (a*m) Mod (i+1)®
	THEN1 all_fc_tac[mod_less_thm]);
a(lemma_tac¬(a*m) Mod (i+1) < (i+1)®
	THEN1 (bc_thm_tac mod_less_thm THEN REPEAT strip_tac));
a(lemma_tac¬m Mod ((a*m) Mod (i + 1)) < (i+1)®
	THEN1 all_fc_tac[less_trans_thm]);
a(ALL_FC_T rewrite_tac [less_div_mod_thm]);
a(contr_tac);
a(DROP_NTH_ASM_T 7 (ante_tac o µ_elim¬q®));
a(asm_rewrite_tac[]);
a(PC_T1 "lin_arith" asm_prove_tac[]);
(* *** Goal "2" *** *)
a(ante_tac(list_µ_elim[ ¬a®, ¬(a*m) Div (i+1)®,
	¬m®, ¬i®, ¬(i+1) Div ((a*m) Mod (i+1))®,
	¬(a*m) Mod (i+1)®,
	¬(i+1) Mod ((a*m) Mod (i+1))® ]gcd_consistent_lemma4));
a(asm_tac (prove_rule[]¬0 < i + 1®));
a(ALL_FC_T rewrite_tac[
	conv_rule(ONCE_MAP_C eq_sym_conv) (arb_gen div_mod_thm)]);
a(lemma_tac¬(i+1) Mod ((a*m) Mod (i+1)) < (a*m) Mod (i+1)®
	THEN1 (bc_thm_tac mod_less_thm THEN REPEAT strip_tac));
a(lemma_tac¬(a*m) Mod (i+1) < (i+1)®
	THEN1 (bc_thm_tac mod_less_thm THEN REPEAT strip_tac));
a(lemma_tac¬(i+1) Mod ((a*m) Mod (i + 1)) < (i+1)®
	THEN1 all_fc_tac[less_trans_thm]);
a(ALL_FC_T rewrite_tac [less_div_mod_thm]);
a(contr_tac);
a(DROP_NTH_ASM_T 7 (ante_tac o µ_elim¬q®));
a(asm_rewrite_tac[]);
a(PC_T1 "lin_arith" asm_prove_tac[]);
val Ûgcd_consistent_lemma5Ý = save_pop_thm"gcd_consistent_lemma5";
=TEX
%%%%
%%%%

=SML
(*
drop_main_goal();
*)
push_consistency_goal ¬Gcd®;
a(prove_¶_tac THEN REPEAT strip_tac);
a(cases_tac¬m' = 0® THEN1 all_var_elim_asm_tac1);
(* *** Goal "1" *** *)
a(¶_tac¬0® THEN REPEAT strip_tac THEN 
	ALL_FC_T rewrite_tac[mod_clauses]);
(* *** Goal "2" *** *)
a(cases_tac¬n' = 0® THEN1 all_var_elim_asm_tac1);
(* *** Goal "2.1" *** *)
a(¶_tac¬0® THEN REPEAT strip_tac THEN 
	ALL_FC_T rewrite_tac[mod_clauses]);
(* *** Goal "2.2" *** *)
a(cases_tac¬m' Mod n' = 0®);
(* *** Goal "2.2.1" *** *)
a(¶_tac¬n'® THEN REPEAT strip_tac
	THEN rewrite_tac[divides_mod_thm]
	THEN ALL_FC_T asm_rewrite_tac[mod_clauses]);
(* *** Goal "2.2.2" *** *)
a(lemma_tac¬0 < m' ± 0 < n' ± 0 < m' Mod n'®
	THEN1 PC_T1 "lin_arith" asm_prove_tac[]
	THEN LIST_DROP_NTH_ASM_T [4, 5, 6] discard_tac);
a(all_fc_tac [gcd_consistent_lemma1]);
a(all_fc_tac [gcd_consistent_lemma5]);
a(¶_tac¬(a*m') Mod n'® THEN asm_rewrite_tac[divides_mod_thm]);
a(µ_tac THEN cases_tac ¬³0 < d® THEN asm_rewrite_tac[]
	THEN REPEAT strip_tac THEN1 all_var_elim_asm_tac1);
a(bc_thm_tac gcd_consistent_lemma2
	THEN REPEAT strip_tac);
val _ = save_consistency_thm ¬Gcd® (pop_thm());
=TEX
%%%%
%%%%

=SML
val Ûgcd_defÝ = save_thm("gcd_def", get_spec¬Gcd®);
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm n· 0 < m ± 0 < n ´ 0 < Gcd m n®);
a(REPEAT strip_tac THEN all_fc_tac[gcd_def]);
a(swap_nth_asm_concl_tac 5);
a(DROP_NTH_ASM_T 3 ante_tac);
a(LEMMA_T ¬Gcd m n = 0® rewrite_thm_tac THEN1 PC_T1"lin_arith" asm_prove_tac[]);
a(rewrite_tac[divides_def] THEN PC_T1 "lin_arith" prove_tac[]);
val Ûgcd_pos_thmÝ = save_pop_thm "gcd_pos_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm n d·
	0 < m ± 0 < n
±	d Divides m ± d Divides n ± Gcd m n Divides d
´	d = Gcd m n
®);
a(REPEAT strip_tac THEN all_fc_tac[gcd_def]
	THEN all_fc_tac[divides_antisym_thm]);
val Ûgcd_unique_thmÝ = save_pop_thm "gcd_unique_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm· 0 < m ´ Gcd m m = m®);
a(REPEAT strip_tac THEN conv_tac eq_sym_conv);
a(bc_thm_tac gcd_unique_thm);
a(asm_rewrite_tac[divides_refl_thm]);
a(ALL_FC_T rewrite_tac[gcd_def]);
val Ûgcd_idemp_thmÝ = save_pop_thm "gcd_idemp_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm n d·
	0 < m ± 0 < n
´	Gcd m n = Gcd n m
®);
a(REPEAT strip_tac THEN bc_thm_tac gcd_unique_thm);
a(REPEAT strip_tac THEN all_fc_tac[arb_gen gcd_def]
	THEN all_fc_tac[arb_gen gcd_def]);
val Ûgcd_comm_thmÝ = save_pop_thm "gcd_comm_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm n d·
	d Divides n 
´	(d Divides (m+n) ¤ d Divides m)®);
a(REPEAT strip_tac);
(* *** Goal "1" *** *)
a(POP_ASM_T ante_tac THEN rewrite_tac[divides_mod_thm]);
a(cases_tac¬d = 0® THEN1 (asm_rewrite_tac[] THEN taut_tac));
a(DROP_NTH_ASM_T 2 ante_tac THEN rewrite_tac[divides_def]);
a(lemma_tac¬0 < d® THEN1 PC_T1 "lin_arith" asm_prove_tac[]);
a(asm_rewrite_tac[] THEN once_rewrite_tac[plus_comm_thm]
	THEN REPEAT strip_tac
	THEN all_var_elim_asm_tac1);
a(POP_ASM_T ante_tac THEN ALL_FC_T rewrite_tac[mod_clauses]);
(* *** Goal "2" *** *)
a(all_asm_ante_tac THEN rewrite_tac[divides_def]
		THEN REPEAT strip_tac
		THEN all_var_elim_asm_tac1);
a(¶_tac¬k' + k® THEN PC_T1 "lin_arith" prove_tac[]);
val Ûdivides_plus_thmÝ = save_pop_thm "divides_plus_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm n d·
	0 < m ± 0 < n
´	Gcd (m + n) n = Gcd m n
®);
a(REPEAT strip_tac THEN bc_thm_tac gcd_unique_thm);
a(lemma_tac¬0 < m + n® THEN1 PC_T1 "lin_arith" asm_prove_tac[]);
a(ALL_FC_T asm_rewrite_tac[gcd_def] THEN REPEAT strip_tac);
(* *** Goal "1" *** *)
a(lemma_tac  ¬Gcd (m + n) n Divides m + n ± Gcd (m + n) n Divides n® 
	THEN1 ALL_FC_T rewrite_tac[gcd_def]);
a(DROP_NTH_ASM_T 2 ante_tac);
a(ALL_FC_T1 fc_¤_canon rewrite_tac[arb_gen divides_plus_thm]);
(* *** Goal "2" *** *)
a(lemma_tac¬Gcd m n Divides m ± Gcd m n Divides n® 
	THEN1 ALL_FC_T rewrite_tac[gcd_def]);
a(lemma_tac¬Gcd m n Divides m + n® 
	THEN1 ALL_FC_T1 fc_¤_canon asm_rewrite_tac[arb_gen divides_plus_thm]);
a(all_fc_tac[gcd_def]);
val Ûgcd_plus_thmÝ = save_pop_thm "gcd_plus_thm";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µm n·
	0 < m ± 0 < n
´	Gcd m n = if m < n then Gcd m (n-m) else if m = n then m else Gcd (m-n) n
®);
a(REPEAT strip_tac);
a(strip_asm_tac (pc_rule1"lin_arith" prove_rule[]
	¬m < n ² m = n ² (³m < n ± ³ m = n ± n < m)®)
	THEN asm_rewrite_tac[]);
(* *** Goal "1" *** *)
a(LEMMA_T ¬m + 1 ¼ n® (strip_asm_tac o
	rewrite_rule[¼_def, pc_rule1"lin_arith" prove_rule[]
	¬µi· (m + 1) + i = (i + 1) + m®])
	THEN1 PC_T1 "lin_arith" asm_prove_tac[]);
a(all_var_elim_asm_tac1);
a(LEMMA_T ¬0 < i + 1® asm_tac THEN REPEAT strip_tac);
a(rewrite_tac[]);
a(ALL_FC_T once_rewrite_tac [gcd_comm_thm]);
a(ALL_FC_T rewrite_tac[arb_gen gcd_plus_thm]);
(* *** Goal "2" *** *)
a(ALL_FC_T rewrite_tac[gcd_idemp_thm]);
(* *** Goal "3" *** *)
a(LEMMA_T ¬n + 1 ¼ m® (strip_asm_tac o
	rewrite_rule[¼_def, pc_rule1"lin_arith" prove_rule[]
	¬µi· (n + 1) + i = (i + 1) + n®])
	THEN1 PC_T1 "lin_arith" asm_prove_tac[]);
a(LIST_DROP_NTH_ASM_T[2, 3, 4] discard_tac
	THEN all_var_elim_asm_tac1);
a(LEMMA_T ¬0 < i + 1® asm_tac THEN REPEAT strip_tac);
a(rewrite_tac[]);
a(ALL_FC_T rewrite_tac[arb_gen gcd_plus_thm]);
val Ûeuclid_algorithm_thmÝ = save_pop_thm "euclid_algorithm_thm";
=TEX
%%%%
%%%%
=SML
(*
drop_main_goal();
*)
push_consistency_goal ¬IndSum‰N®;
a(¶_tac ¬Ìa f·SetFold 0 ($ +) f a®);
a(µ_tac THEN rewrite_tac[] THEN bc_thm_tac(set_fold_def));
a(rewrite_tac[plus_assoc_thm]);
val _ = save_consistency_thm ¬IndSum‰N® (pop_thm());
val Ûind_sum_î_defÝ = save_thm("ind_sum_î_def", get_spec ¬IndSum‰N®);
=TEX
%%%%
%%%%
=SML
set_goal([], ¬µ f·
	“ {} f = 0
±	(µ x a· a  Finite ± ³ x  a
´	“ ({x} À a) f = f x + “ a f)®);
a(REPEAT strip_tac THEN1 rewrite_tac[ind_sum_î_def]);
a(lemma_tac¬a ¡ {x|³ f x = 0}  Finite®
	THEN1 (bc_thm_tac ¡_finite_thm THEN REPEAT strip_tac));
a(ALL_FC_T rewrite_tac[ind_sum_î_def]);
val Ûind_sum_î_def1Ý = save_pop_thm "ind_sum_î_def1";
=TEX
%%%%
%%%%
=SML
set_goal([], ¬µ f·
	“ {} f = 0
±	({x|³ f x = 0}  Finite
	´	(µ x a· ³ x  a
	´	“ ({x} À a) f = f x + “ a f))®);
a(REPEAT strip_tac THEN1 rewrite_tac[ind_sum_î_def]);
a(lemma_tac¬a ¡ {x|³ f x = 0}  Finite®
	THEN1 (bc_thm_tac ¡_finite_thm THEN REPEAT strip_tac));
a(ALL_FC_T rewrite_tac[ind_sum_î_def]);
val Ûind_sum_î_def2Ý = save_pop_thm "ind_sum_î_def2";
=TEX
%%%%
%%%%
=SML
(*
drop_main_goal();
*)
push_consistency_goal ¬IndProd‰N®;
a(¶_tac ¬Ìa f·SetFold 1 ($ *) f a®);
a(µ_tac THEN rewrite_tac[] THEN bc_thm_tac(set_fold_def));
a(PC_T1 "lin_arith" prove_tac[]);
val _ = save_consistency_thm ¬IndProd‰N® (pop_thm());
val Ûind_prod_î_defÝ = save_thm("ind_prod_î_def", get_spec ¬IndProd‰N®);
=TEX
%%%%
%%%%
=SML
set_goal([], ¬µ f·
	 {} f = 1
±	(µ x a· a  Finite ± ³ x  a
´	 ({x} À a) f = f x *  a f)®);
a(REPEAT strip_tac THEN1 rewrite_tac[ind_prod_î_def]);
a(lemma_tac¬a ¡ {x|³ f x = 1}  Finite®
	THEN1 (bc_thm_tac ¡_finite_thm THEN REPEAT strip_tac));
a(ALL_FC_T rewrite_tac[ind_prod_î_def]);
val Ûind_prod_î_def1Ý = save_pop_thm "ind_prod_î_def1";
=TEX
%%%%
%%%%
=SML
set_goal([], ¬µ f·
	 {} f = 1
±	({x|³ f x = 1}  Finite
	´	(µ x a· ³ x  a
	´	 ({x} À a) f = f x *  a f))®);
a(REPEAT strip_tac THEN1 rewrite_tac[ind_prod_î_def]);
a(lemma_tac¬a ¡ {x|³ f x = 1}  Finite®
	THEN1 (bc_thm_tac ¡_finite_thm THEN REPEAT strip_tac));
a(ALL_FC_T rewrite_tac[ind_prod_î_def]);
val Ûind_prod_î_def2Ý = save_pop_thm "ind_prod_î_def2";
=TEX
%%%%
%%%%

=TEX
=SML
val Ûmod_3_plus_thmÝ = rewrite_rule[](all_µ_intro(list_µ_elim[¬m:î®, ¬n:î®, ¬3®]mod_plus_homomorphism_thm));
=TEX
=SML
val Ûmod_3_times_thmÝ = rewrite_rule[](all_µ_intro(list_µ_elim[¬m:î®, ¬n:î®, ¬3®]mod_times_homomorphism_thm));
=TEX
=SML
val Ûmod_3_î_exp_thmÝ = rewrite_rule[](all_µ_intro(list_µ_elim[¬m:î®, ¬n:î®, ¬3®]mod_î_exp_thm));
=TEX
=SML
val Ûmod_3_clausesÝ = rewrite_rule[](µ_elim¬3® mod_clauses);
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µdigits n·
	“ {i | i < n} (Ìi·digits i * 10 ^ i) Mod 3 =
	“ {i | i < n} (Ìi·digits i) Mod 3
®);
a(REPEAT strip_tac);
a(induction_tac¬n®);
(* *** Goal "1" *** *)
a(rewrite_tac[ind_sum_î_def1, pc_rule1"sets_ext1" prove_rule[]¬{x|F} = {}®]);
(* *** Goal "1" *** *)
a(PC_T1 "predicates" lemma_tac¬{i|i < n + 1} = {n} À {i|i < n} ± ³n  {i|i < n}
	± {i|i < n}  Finite®
	THEN1 rewrite_tac[range_finite_size_thm]);
(* *** Goal "2.1" *** *)
a(PC_T "sets_ext1" strip_tac THEN REPEAT strip_tac
	THEN PC_T1 "lin_arith" asm_prove_tac[]);
(* *** Goal "2.2" *** *)
a(ALL_FC_T asm_rewrite_tac[ind_sum_î_def1]);
a(once_rewrite_tac[mod_3_plus_thm]);
a(asm_rewrite_tac[]);
a(once_rewrite_tac[mod_3_times_thm]);
a(once_rewrite_tac[mod_3_î_exp_thm]);
a(LEMMA_T¬1 ^ n = 1® (fn th => rewrite_tac[th, mod_3_clauses]));
a(DROP_ASMS_T discard_tac
	THEN induction_tac¬n® THEN asm_rewrite_tac[î_exp_def]);
val Ûdiv_3_rule_thm1Ý = save_pop_thm "div_3_rule_thm1";
=TEX
%%%%
%%%%

=SML
set_goal([], ¬µdigits n·
	3 Divides “ {i | i < n} (Ìi·digits i * 10 ^ i) ¤
	3 Divides “ {i | i < n} (Ìi·digits i)
®);
a(REPEAT µ_tac);
a(rewrite_tac[divides_mod_thm, div_3_rule_thm1]);
val Ûdiv_3_rule_thmÝ = save_pop_thm "div_3_rule_thm";
=TEX
%%%%
%%%% REAL INTEGRAL DOMAINS
%%%%
=SML
set_goal([], ¬Universe  RealID®);
a(rewrite_tac[real_i_d_def]);
val Û¯_real_i_d_thmÝ = save_pop_thm "¯_real_i_d_thm";
=TEX
%%%%
%%%%
=SML
set_goal([], ¬µV· V € RealID ´ ¥V  RealID®);
a(PC_T1 "sets_ext1" rewrite_tac[real_i_d_def]
	THEN REPEAT strip_tac
	THEN all_asm_fc_tac[] THEN all_asm_fc_tac[]);
val Û¥_real_i_d_thmÝ = save_pop_thm "¥_real_i_d_thm";
=TEX
%%%%
%%%%
=SML
set_goal([], ¬ú  RealID®);
a(rewrite_tac[ú_def]);
a(bc_thm_tac ¥_real_i_d_thm THEN rewrite_tac[]);
val Ûú_real_i_d_thmÝ = save_pop_thm "ú_real_i_d_thm";
=TEX
%%%%
%%%%
=SML
set_goal([], ¬ú = {x | ¶i·x = ú¯ i}®);
a(PC_T1 "sets_ext1" rewrite_tac[ú_def, real_i_d_def]
	THEN REPEAT strip_tac);
(* *** Goal "1" *** *)
a(swap_nth_asm_concl_tac 1 THEN REPEAT strip_tac);
a(¶_tac ¬{x | ¶i·x = ú¯ i}® THEN rewrite_tac[]
	THEN REPEAT strip_tac
	THEN_TRY all_var_elim_asm_tac1);
(* *** Goal "1.1" *** *)
a(¶_tac¬îú 1® THEN rewrite_tac[ú¯_îú_thm]);
(* *** Goal "1.2" *** *)
a(¶_tac¬i + i'® THEN rewrite_tac[ú¯_plus_homomorphism_thm]);
(* *** Goal "1.3" *** *)
a(¶_tac¬~i® THEN rewrite_tac[ú¯_minus_thm]);
(* *** Goal "1.4" *** *)
a(¶_tac¬i * i'® THEN rewrite_tac[ú¯_times_homomorphism_thm]);
(* *** Goal "1.5" *** *)
a(asm_rewrite_tac[]);
(* *** Goal "2" *** *)
a(all_var_elim_asm_tac1);
a(ú_induction_tac¬i®);
(* *** Goal "2.1" *** *)
a(asm_rewrite_tac[ú¯_îú_thm]);
(* *** Goal "2.2" *** *)
a(rewrite_tac[ú¯_minus_thm] THEN all_asm_fc_tac[]);
(* *** Goal "2.3" *** *)
a(rewrite_tac[ú¯_plus_homomorphism_thm] THEN all_asm_fc_tac[]);
val Ûú_thmÝ = save_pop_thm "ú_thm";
=TEX
%%%%
%%%% FIELDS
%%%%
=SML
set_goal([], ¬Universe  RealField®);
a(rewrite_tac[real_field_def, ¯_real_i_d_thm]);
val Û¯_real_field_thmÝ = save_pop_thm "¯_real_field_thm";
=TEX
%%%%
%%%%
=SML
set_goal([], ¬µV· V € RealField ´ ¥V  RealField®);
a(PC_T1 "sets_ext1" rewrite_tac[real_field_def,
	real_i_d_def]
	THEN REPEAT strip_tac
	THEN all_asm_fc_tac[] THEN all_asm_fc_tac[]);
val Û¥_real_field_thmÝ = save_pop_thm "¥_real_field_thm";
=TEX
%%%%
%%%%
=SML
set_goal([], ¬%bbQ%  RealField®);
a(rewrite_tac[rat_def]);
a(bc_thm_tac ¥_real_field_thm THEN rewrite_tac[]);
val Ûrat_real_i_d_thmÝ = save_pop_thm "rat_real_i_d_thm";
=TEX
%%%%
%%%%
=SML
set_goal([], ¬ú € %bbQ%®);
a(PC_T1"sets_ext1" rewrite_tac[rat_def, ú_def, real_field_def]
	THEN REPEAT strip_tac
	THEN all_asm_fc_tac[]);
val Ûú_€_rat_thmÝ = save_pop_thm "ú_€_rat_thm";
=TEX
%%%%
%%%%
=SML
set_goal([], ¬µA· FieldOfFractions A  RealField®);
a(REPEAT strip_tac THEN rewrite_tac[field_of_fractions_def]);
a(bc_thm_tac ¥_real_field_thm);
a(PC_T1 "sets_ext1" prove_tac[]);
val Ûfield_of_fractions_field_thmÝ = save_pop_thm "field_of_fractions_field_thm";
=TEX
%%%%
%%%%
=SML
set_goal([], ¬%bbQ% = FieldOfFractions ú®);
a(rewrite_tac[field_of_fractions_def, rat_def]);
a(PC_T "sets_ext1" strip_tac THEN REPEAT strip_tac
	THEN1 all_asm_fc_tac[]);
a(DROP_NTH_ASM_T 2 bc_thm_tac THEN REPEAT strip_tac);
a(POP_ASM_T ante_tac
	THEN PC_T1"sets_ext1" rewrite_tac[ú_def, real_field_def]
	THEN REPEAT strip_tac
	THEN all_asm_fc_tac[]);
val Ûrat_field_of_fractions_thmÝ = save_pop_thm "rat_field_of_fractions_thm";
=TEX
%%%%
%%%%
=SML
set_goal([], ¬µA·
	A  RealID
´	FieldOfFractions A =
	{x | ¶a b·a  A ± b  A ± ³b = î¯ 0 ± x = a * b ›-›1}®);
a(PC_T1 "sets_ext1" rewrite_tac[real_i_d_def, field_of_fractions_def,
	real_field_def] THEN REPEAT strip_tac);
(* *** Goal "1" *** *)
a(LEMMA_T ¬x  {y | ¶ a b· a  A ± b  A ± ³ b = î¯ 0 ± y = a * b ›-›1}®
	(fn th => ante_tac th THEN rewrite_tac[]));
a(POP_ASM_T bc_thm_tac THEN REPEAT strip_tac
	THEN_TRY all_var_elim_asm_tac1);
(* *** Goal "1.1" *** *)
a(¶_tac¬î¯ 1® THEN ¶_tac¬î¯ 1® THEN asm_rewrite_tac[]);
(* *** Goal "1.2" *** *)
a(¶_tac¬a * b' + a' * b® THEN ¶_tac¬b*b'®);
a(asm_rewrite_tac[¯_times_eq_0_thm]);
a(REPEAT strip_tac
	THEN_TRY LIST_GET_NTH_ASM_T [7, 8, 9] bc_tac
	THEN REPEAT strip_tac);
a(DROP_NTH_ASM_T 4 (fn th =>
	ALL_FC_T1 fc_¤_canon
	once_rewrite_tac[conv_rule(ONCE_MAP_C (RIGHT_C eq_sym_conv))
	¯_times_cancel_thm] THEN asm_tac th));
a(DROP_NTH_ASM_T 2 (fn th =>
	ALL_FC_T1 fc_¤_canon
	once_rewrite_tac[conv_rule(ONCE_MAP_C (RIGHT_C eq_sym_conv))
	¯_times_cancel_thm] THEN asm_tac th));
a(ALL_FC_T rewrite_tac[¯_recip_clauses]);
a(rewrite_tac[¯_times_plus_distrib_thm, ¯_times_assoc_thm]);
a(rewrite_tac[pc_rule1 "¯_lin_arith" prove_rule[]
	¬µa b c d·a*b ›-›1*c*d = (a*c)*(b ›-›1 *d)®]);
a(ALL_FC_T rewrite_tac[¯_recip_clauses]);
a(conv_tac (RANDS_C ¯_anf_conv) THEN strip_tac);
(* *** Goal "1.3" *** *)
a(¶_tac¬~a® THEN ¶_tac¬b® THEN 
	LIST_DROP_NTH_ASM_T [5] (ALL_FC_T asm_rewrite_tac));
a(PC_T1 "¯_lin_arith" prove_tac[]);
(* *** Goal "1.4" *** *)
a(¶_tac¬a*a'® THEN ¶_tac¬b*b'®);
a(asm_rewrite_tac[¯_times_eq_0_thm]);
a(LIST_DROP_NTH_ASM_T [7] (ALL_FC_T asm_rewrite_tac));
a(ALL_FC_T rewrite_tac[¯_recip_clauses]
	THEN PC_T1 "¯_lin_arith" prove_tac[]);
(* *** Goal "1.5" *** *)
a(POP_ASM_T (strip_asm_tac o rewrite_rule[¯_times_eq_0_thm]));
a(ALL_FC_T rewrite_tac[¯_recip_clauses]);
a(¶_tac¬b® THEN ¶_tac¬a® THEN asm_rewrite_tac[]);
a(PC_T1 "¯_lin_arith" prove_tac[]);
(* *** Goal "1.6" *** *)
a(¶_tac¬x® THEN ¶_tac¬î¯ 1® THEN asm_rewrite_tac[]);
(* *** Goal "2" *** *)
a(all_var_elim_asm_tac1);
a(LIST_DROP_NTH_ASM_T [2, 3] bc_tac
	THEN REPEAT strip_tac
	THEN DROP_NTH_ASM_T 1 bc_thm_tac
	THEN REPEAT strip_tac);
val Ûfield_of_fractions_thmÝ = save_pop_thm "field_of_fractions_thm";
=TEX
%%%%
%%%%
=SML
set_goal([], ¬%bbQ% = {x | ¶i:ú; m:î·x = ú¯ i * î¯ (m+1) ›-›1}®);
a(rewrite_tac[rat_field_of_fractions_thm]);
a(strip_asm_tac ú_real_i_d_thm);
a(ALL_FC_T rewrite_tac[field_of_fractions_thm]);
a(rewrite_tac[ú_thm]);
a(PC_T1 "sets_ext1" REPEAT strip_tac THEN all_var_elim_asm_tac1);
(* *** Goal "1" *** *)
a(strip_asm_tac(µ_elim¬i'®ú_cases_thm1)
	THEN all_var_elim_asm_tac1);
(* *** Goal "1.1" *** *)
a(POP_ASM_T ante_tac THEN rewrite_tac[ú¯_îú_thm,
	î¯_one_one_thm,
	pc_rule1 "lin_arith" prove_rule[]
	¬(³m = 0 ¤ 1 ¼ m) ± µi·1 + i = i + 1®,
	¼_def] THEN REPEAT strip_tac THEN all_var_elim_asm_tac1);
a(¶_tac¬i® THEN ¶_tac¬i'® THEN rewrite_tac[]);
(* *** Goal "1.2" *** *)
a(¶_tac¬~i® THEN ¶_tac¬m® THEN rewrite_tac[ú¯_minus_thm,
	ú¯_îú_thm]);
a(lemma_tac¬³î¯ (m + 1) = î¯ 0®
	THEN1 rewrite_tac[î¯_one_one_thm]);
a(ALL_FC_T rewrite_tac[¯_minus_recip_thm]);
a(PC_T1 "¯_lin_arith" prove_tac[]);
(* *** Goal "2" *** *)
a(¶_tac¬ú¯ i® THEN ¶_tac¬ú¯(îú(m+1))® THEN
	rewrite_tac[ú¯_îú_thm, î¯_one_one_thm]);
a(strip_tac THEN1 prove_tac[]);
a(¶_tac¬îú(m+1)® THEN rewrite_tac[ú¯_îú_thm]);
val Ûrat_thmÝ = save_pop_thm "rat_thm";
=TEX
\Hide{
=SML
local 
	open	ListerSupport;
	val ÛsectionsÝ = [LSBanner, LSThms];
	val {print=pt, out=ot, out1=ot1} = gen_theory_lister sections;
	fun output_banner (thyn : string) = "THEOREMS IN THE THEORY " ^ (case thyn of "-" => get_current_theory_name () | _ => thyn);
	fun output_theorems (par : {out_file:string, theory:string}) : unit = (
		(ot output_banner par) handle ex => reraise ex "z_output_theory"
	);
in
	val _ = output_theorems{out_file="wrk074.th.doc", theory="-"};
end;
=TEX
} %\Hide
\end{document}

=TEX
%%%%
%%%%
=IGN
